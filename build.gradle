apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.0'
        classpath "eu.appsatori:gradle-fatjar-plugin:0.3"
    }
}

apply plugin: 'com.github.kt3k.coveralls'
apply plugin: "eu.appsatori.fatjar"

sourceCompatibility = 1.8
version = '0.0.2'

repositories {
    mavenCentral()
    maven{
        url 'http://maven.restlet.org'
    }
}

dependencies {
    //compile dependencies
    compile group: 'org.jetbrains', name: 'annotations', version: '13.0'
    compile group: 'org.activiti', name: 'activiti-engine', version: '5.16.4'
    compile group: 'org.activiti', name: 'activiti-spring', version: '5.16.4'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '0.7.1'
    compile group: 'io.dropwizard', name: 'dropwizard-client', version: '0.7.1'
    compile group: 'io.dropwizard', name: 'dropwizard-auth', version: '0.7.1'
    compile group: 'org.springframework', name: 'spring-web', version: '4.0.6.RELEASE'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.31'
    compile group: 'com.h2database', name: 'h2', version: '1.4.178'

    //test dependencies
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '0.7.1'
    testCompile group: 'org.slf4j', name: 'log4j-over-slf4j', version: '1.7.6' //NoSuchFieldError fix
}

jacocoTestReport {
    dependsOn test
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
        html.destination "${buildDir}/jacocoHtml"
    }
}

fatJar {
    manifest {
        attributes 'Implementation-Title': 'Activiti Console',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Main-Class': 'pl.edu.pw.ii.ActivitiConsole.ActivitiConsoleApplication'
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

fatJar.mustRunAfter jar
assemble.dependsOn fatJar

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}